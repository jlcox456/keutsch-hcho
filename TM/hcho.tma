State Init {
      > Telemetry Start
  +10 > Log Initiating PPS Synchronization
      Validate PPS_synch;
}

State PPS_synch nolog {
  { if (PPS_drift > 0 && PPS_drift < 0.001) {
      Validate PPS_rate_synch;
    } else if (PPS_drift < 0) {
      Validate PPS_synch2;
    }
  }
  +1 > PPS Offset Adjust 30000
  +4 Validate PPS_synch;
}

State PPS_synch2 nolog {
  +1 { if (PPS_drift > -0.075) Validate PPS_synch3; }
  +1 > PPS Offset Adjust 30000
  +4 Validate PPS_synch;
}

State PPS_synch3 nolog {
   +1 { double adj;
        if (PPS_drift > 0) Validate PPS_rate_synch;
        else {
          unsigned short adju;
          adj = (0.0005-PPS_drift)*400000;
          if (adj > 30000) adj = 30000;
          adju = adj;
          ci_sendfcmd(0, "PPS Offset Adjust %u\n", adju);
        }
      }
   +6 Validate PPS_synch3;
}

%{
  double PPS_integral;
  #define GP 1.1e7
  #define GI 1e6
  #define PPS_CLIP_LIMIT 30000
  #define PPS_INT_LIMIT (PPS_CLIP_LIMIT/GI)
  /* UpdatePeriod = 5; */
%}

State PPS_rate_synch {
  {
    PPS_integral = PPS_integral + PPS_min_drift;
    if (PPS_integral > PPS_INT_LIMIT) PPS_integral = PPS_INT_LIMIT;
    else if (PPS_integral < -PPS_INT_LIMIT) PPS_integral = -PPS_INT_LIMIT;
  }
  { double rate_adj;
    rate_adj = PPS_min_drift * GP + PPS_integral * GI;
    if (rate_adj > PPS_CLIP_LIMIT) rate_adj = PPS_CLIP_LIMIT;
    else if (rate_adj < -PPS_CLIP_LIMIT) rate_adj = -PPS_CLIP_LIMIT;
    if (rate_adj != PPS_fine)
      ci_sendfcmd(2, "PPS Rate Adjust %.0lf\n", rate_adj);
  }
}


